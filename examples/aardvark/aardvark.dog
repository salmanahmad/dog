#!/usr/bin/env dog

# ===================
# = Data Structures =
# ===================

IMPORT FUNCTION "classify.rb" AS classify

CONFIG default_community = "vark"

vark = community {
  string location
  array expertise
}

# ===================
# = Human Functions =
# ===================

indicate_willingness = task {
  string input instructions
  string output response
}

answer_question = task {
  string input instructions
  string output response
}

questions = event {
  input string body
}

# ========
# = Main =
# ========

LISTEN TO vark VIA gtalk FOR questions

ON question DO
  asker = PERSON FROM question
  
  NOTIFY asker OF "Hey! I'll try to find someone who can answer that for you."
  
  category = COMPUTE classify ON question.body
  ranked_users = PEOPLE FROM vark WHERE expertise CONTAINS category
  
  answered = false
  
  FOR user IN ranked_users DO
    response = ASK user VIA gtalk TO indicate_willingness USING instructions = "Hi! I have a question for you about *{category}*. Do you have time to answer it?"
    potential_answerer = PERSON FROM response
    IF response == "yes"
      NOTIFY potential_answerer VIA gtalk OF "Great, here it is:"
      response = ASK potential_answerer VIA gtalk TO answer_question USING instructions = question
      
      WAIT ON response
      
      answered = true
      NOTIFY potential_answerer VIA gtalk OF "Thanks so much. I'll send that along."
      NOTIFY asker VIA gtalk OF "I got an answer for you from *#{potential_answerer}*."
      NOTIFY asker VIA gtalk OF response
    ELSE
      NOTIFY potential_answerer VIA gtalk OF "Okay, no problem!"
      
    END
  UNTIL answered
  
  IF !answered THEN
    NOTIFY asker VIA gtalk OF "Sorry! I could not find anyone right now. Please try again."
  END
  
END

