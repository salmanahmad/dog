

CONFIG default_community = "learners"

learners = community {
  string objective
  array teachables
  array learnables
}

provide_three_interests = task {
  instructions = "Please provide three things that you are interested in learning and teaching."
  output string objective
  output array learnables
  output array teachables
}

find_teacher = event {
  input string learnable
  output array teachers
}

matched_teachers = event {
  output array teachers
}

meeting_conversation = event {
  input person person
}

chat = task {
  output string message
}

DEFINE FUNCTION converse FOR people DO
  messages = ASK people TO chat REPEAT INDEFINITELY
  
  ON message IN messages DO
    NOTIFY people OF message.message
  END
  
  WAIT FOREVER
END

LISTEN TO learners VIA http FOR find_teacher
LISTEN TO learners VIA http FOR matched_teachers
LISTEN TO learners VIA http AT '/meet' FOR meeting_conversation

ON request IN find_teacher DO
  user = PERSON FROM request
  REPLY WITH request.teachers = PEOPLE FROM learners WHERE teachables IN request.learnable
END

ON request IN matched_teachers DO
  user = PERSON FROM request
  REPLY WITH request.teachers = PEOPLE FROM learners WHERE teachables IN user.learners.learnables
END

ON request IN meeting_conversation DO
  requester = PERSON FROM request
  requestee = request.person
  ASK [requester, requestee] TO converse
END

ON request IN dog.account.create DO
  new_user = PERSON FROM request
  interests = ASK new_user TO provide_three_interests
  
  new_user.learners.objective = interests.objective
  new_user.learners.learnables = interests.learnables
  new_user.learners.teachables = interests.teachables
END

